// Forcepoint Web Proxy Logs
// Last Updated Date: April 29 2022
//
// This parser parses Syslog CEF events for Forcepoint Web Proxy logs.
//
// Disposition Code Reference Guide - http://www.websense.com/content/support/library/web/v76/siem/siem.pdf
// The guide is dated 2014, so may not be the most up to date version
//
// Parser Notes:
// 1. This parser assumes logs are collected into the CommonSecurityLog table.
//
// Usage Instruction : 
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name and alias.
// Functions usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. ForcepointWebProxy | take 10).
//
//
CommonSecurityLog
| where DeviceVendor == 'Forcepoint' and DeviceProduct has "Security"
// Convert Receipttime value 
| extend SourceTime = unixtime_milliseconds_todatetime(tolong(ReceiptTime))
// Extract values from AdditionalExtensions column - Username, Reason, LogRecordSource (which is not used) 
| extend AdditionalExtensions = extract_all(@"(?P<key>\w+)=(?P<value>[a-zA-Z0-9-_:/@. ]+)", dynamic(["key","value"]), AdditionalExtensions)
| mv-apply AdditionalExtensions on (
    summarize AdditionalExtensionsExtracted = make_bag(pack(tostring(AdditionalExtensions[0]), AdditionalExtensions[1]))
)
| extend DvcAction = case(Activity has 'permitted', "Allow", Activity has 'blocked', "Deny","-"),
    DispositionCode=tostring(DeviceCustomNumber1), 
    DstBytes=ReceivedBytes,
    DstHostname=DestinationHostName,
    DstIpAddr=DestinationIP,
    DstPortNumber=DestinationPort,
    HttpContentType=DeviceCustomString3, 
    HttpRequestMethod=RequestMethod,
    HttpUserAgent=RequestClientApplication,
    NetworkApplicationProtocol=ApplicationProtocol,
    Policy=DeviceCustomString1, 
    Reason=AdditionalExtensionsExtracted.reason, 
    SrcBytes=SentBytes,
    SrcIpAddr=SourceIP,
    SrcPortNumber=SourcePort,
    UrlCategory=DeviceEventClassID, 
    Username=AdditionalExtensionsExtracted.loginID 
//
// Is there value in including DeviceAction, SimplifiedDeviceAction and Activity 
// when all three refer to whether or not something allowed or blocked?
// Excluding LogSeverity as it is the code relating to DvcAction
//
| project TimeGenerated, SourceTime, Username, Reason, UrlCategory, Computer, DstPortNumber, DstIpAddr, DeviceAddress, SrcPortNumber, SrcIpAddr, DvcAction, NetworkApplicationProtocol, DstHostname, DstBytes, SrcBytes, RequestURL, HttpUserAgent, HttpRequestMethod, SourceUserName, Policy, DispositionCode, ['Scan Duration (ms)']=DeviceCustomNumber2, HttpContentType
